StepView = require '../step'
passwordHelper = require '../../lib/password_helper'
_ = require 'underscore'


module.exports = class PasswordView extends StepView

    template: require '../templates/view_steps_password'

    events:
        'click .next': 'onSubmit'
        'click [action=password-visibility]': 'onToggleVisibility'

        'change input': 'checkPasswordStrength'
        'input input': 'checkPasswordStrength'
        'keyup input': 'checkPasswordStrength'
        'mouseup input': 'checkPasswordStrength'
        'paste input': 'checkPasswordStrength'

    isVisible: false


    onRender: (args...) ->
        super args...
        @$inputPassword = @$('input[name=password]')
        @$visibilityButton = @$('[action=password-visibility]')
        @$strengthBar = @$('progress')

        if @error
            @showError(@error, false)
        else
            @hideError()


    renderInput: =>
        data = @serializeInputData()

        # Show/hide password value
        @$inputPassword.attr 'type', data.inputType

        # Update Button title
        @$visibilityButton.html(t(data.visibilityTxt))


    initialize: (args...) ->
        super args...
        # lowest level is 1 to display a red little part
        @passwordStrength = passwordHelper.getStrength ''
        @updatePasswordStrength = updatePasswordStrength.bind(@)


    updatePasswordStrength= _.throttle( ->
        @passwordStrength = passwordHelper.getStrength @$inputPassword.val()

        if @passwordStrength.percentage is 0
            @passwordStrength.percentage = 1
        @$strengthBar.attr 'value', @passwordStrength.percentage
        @$strengthBar.attr 'class', 'pw-' + @passwordStrength.label
        @$inputPassword.removeClass('error')
    , 500)


    checkPasswordStrength: ->
        @updatePasswordStrength()


    # Get 1rst error only
    # err is an object such as:
    # { type: 'password', text:'step password empty'}
    serializeData: () ->
        return Object.assign {}, @serializeInputData(), {
            id:         "#{@model.get 'name'}-figure"
            figureid:   require '../../assets/sprites/icon-cozy.svg'
            badgeId: require '../../assets/sprites/icon-shield-24.svg'
            passwordStrength: @passwordStrength
        }


    serializeInputData: =>
        visibilityAction = if @isVisible then 'hide' else 'show'
        type = if @isVisible then 'text' else 'password'
        {
            visibilityTxt:  "step password #{visibilityAction}"
            inputType:      type
        }


    onToggleVisibility: (event) ->
        event?.preventDefault()

        # Update Visibility
        @isVisible = not @isVisible

        # Update Component
        @renderInput()


    getDataForSubmit: ->
        return {
            password: @$inputPassword.val()
        }


    onSubmit: (event)->
        data = @getDataForSubmit()
        validation = @model.validate data
        if not validation.success
            event.preventDefault()
            errors = validation.errors
            if errors?.password
                @$inputPassword.addClass('error')
                @showError(errors.password)
        else
            super event, data
